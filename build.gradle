plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
    id "maven-publish"
}

ext {
    buildNumber = "build.${System.getenv().BUILD_NUMBER}" ?: "local"
    local = buildNumber == "local"
}

group "org.bookmc"
version "0.0.1+$buildNumber"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1"

    implementation "com.dropbox.mobile.store:store4:4.0.2-KT15"

    implementation "io.ktor:ktor-client-gson:1.6.2"
    implementation "io.ktor:ktor-client-core:1.6.2"
    implementation "io.ktor:ktor-client-apache:1.6.2"

    implementation "org.json:json:20210307"
    implementation "com.google.code.gson:gson:2.8.7"
}

compileKotlin.kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                url = "https://github.com/BookMC/${project.name}"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}